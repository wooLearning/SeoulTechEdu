`timescale 1ns / 1ps

// ALU shared by R-type, I-type, and load/store address generation.
module Alu (
  input  logic [31:0]            iA,
  input  logic [31:0]            iB,
  input  rv32i_pkg::alu_op_e     iAluOp,
  output logic [31:0]            oResult
);

  always_comb begin
    oResult = 32'd0;

    unique case (iAluOp)
      // Shift amount uses the low 5 bits, matching RV32I.
      rv32i_pkg::ALU_ADD:  oResult = iA + iB;
      rv32i_pkg::ALU_SUB:  oResult = iA - iB;
      rv32i_pkg::ALU_SLL:  oResult = iA << iB[4:0];
      rv32i_pkg::ALU_SLT:  oResult = {31'd0, ($signed(iA) < $signed(iB))};
      rv32i_pkg::ALU_SLTU: oResult = {31'd0, (iA < iB)};
      rv32i_pkg::ALU_XOR:  oResult = iA ^ iB;
      rv32i_pkg::ALU_SRL:  oResult = iA >> iB[4:0];
      rv32i_pkg::ALU_SRA:  oResult = $signed(iA) >>> iB[4:0];
      rv32i_pkg::ALU_OR:   oResult = iA | iB;
      rv32i_pkg::ALU_AND:  oResult = iA & iB;
      default: oResult = 32'd0;
    endcase
  end

endmodule
